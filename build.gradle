buildscript {
    ext.kotlin_version = '1.2.31'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin'
apply plugin: 'application'

group 'eu.cqse'
version '1.0.0'

repositories {
    mavenCentral()
    jcenter()
    ivy {
        url "https://share.cqse.eu/svn/artifacts/ivy"
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    compile group: "org.http4k", name: "http4k-core", version: "3.14.0"
    compile group: "org.http4k", name: "http4k-server-jetty", version: "3.14.0"
    compile group: "org.http4k", name: "http4k-client-okhttp", version: "3.14.0"
    compile group: "org.http4k", name: "http4k-multipart", version: "3.14.0"

    compile 'com.github.ajalt:clikt:1.1.0'

    compile 'org.apache.ant:ant:1.10.3'

    compile 'io.github.microutils:kotlin-logging:1.4.7'
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.10.0'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.10.0'
    compile group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.10.0'

    compile 'org.conqat:org.conqat.lib.commons:0.20160822'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

processResources {
    filesMatching('**/app.properties') {
        filter {
            it.replace('%APP_VERSION_TOKEN_REPLACED_DURING_BUILD%', version.toString())
        }
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Teamscale HTTP File Upload',
                'Implementation-Version': version,
                'Main-Class': "eu.cqse.fileupload.MainKt"
    }
    baseName = 'teamscale-fileupload'
    // make it a fat jar by including all runtime dependencies
    from { configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) } }
}

task doc(type: Exec) {
    doFirst {
        mkdir 'build/doc'
    }

    workingDir 'resources/userguide'
    commandLine 'pandoc', 'userguide.md', '-o', '../../build/doc/userguide.pdf', '--latex-engine=xelatex',
            '--toc', '--template=eisvogel.tex', '--listings', '--number-sections'
}

task prepareDist(dependsOn: [jar, doc], type: Copy) {
    from('build/libs') {
        include '*.jar'
    }
    from('build/doc') {
        include '*.pdf'
    }
    from('resources') {
        include 'linux'
        include 'windows'
    }
    from('build/resources/main') {
        include 'log4*.xml'
    }
    into 'build/teamscale-fileupload'
}

task dist(dependsOn: ['check', prepareDist], type: Zip) {
    baseName 'teamscale-fileupload'
    from 'build'
    include 'teamscale-fileupload/**'
}

